MRS_mo_fa_zhi_sen_nei_zhan = {
  # 内战爆发
  MRS_nei_zhan_bao_fa = {
    activation = {
        has_global_flag = MRS_civil_war_prepare_flag
        NOT = {
          has_global_flag = MRS_civil_war_flag
        }
        NOT = {
          OR = {
            MRS = {
              has_war_with = ALI
            }
            MRS = {
              has_war_with = NRM
            }
          }
        }
    }
    available = {
      always = no
    }
    cancel_trigger = {
      OR = {
        MRS = {
          has_war_with = ALI
        }
        MRS = {
          has_war_with = NRM
        }
      }
    }
    icon = mission_icon     # For GFX_decision_mission_icon
    is_good = no
    war_with_on_timeout = MRS
    war_with_on_timeout = ALI
    war_with_on_timeout = NRM
    days_mission_timeout = 30
    selectable_mission = no
    complete_effect = {
      
    }
    timeout_effect = {
      hidden_effect = {
        news_event = {
          id = mrs.112
        }
      }
      if = {
        limit = {
          tag = MRS
        }
        if = {
          limit = {
            country_exists = ALI
          }
          declare_war_on = {
            target = ALI
            type = MRS_Civil_War
          }
          if = {
            limit = {
              country_exists = NRM
            }
            ALI = {
              declare_war_on = {
                target = NRM
              }
            }
          }
        }
        if = {
          limit = {
            country_exists = NRM
          }
          declare_war_on = {
            target = NRM
            type = MRS_Civil_War
          }
        }
      }
      if = {
        limit = {
          tag = ALI
        }
        if = {
          limit = {
            country_exists = MRS
          }
          MRS = {
            declare_war_on = {
              target = ALI
              type = MRS_Civil_War
            }
          }
          if = {
            limit = {
              country_exists = NRM
            }
            MRS = {
              declare_war_on = {
                target = NRM
                type = MRS_Civil_War
              }
            }
          }
        }
        if = {
          limit = {
            country_exists = NRM
          }
          declare_war_on = {
            target = NRM
            type = MRS_Civil_War
          }
        }
      }
      if = {
        limit = {
          tag = NRM
        }
        if = {
          limit = {
            country_exists = ALI
          }
          declare_war_on = {
            target = ALI
            type = MRS_Civil_War
          }
          if = {
            limit = {
              country_exists = MRS
            }
            ALI = {
              declare_war_on = {
                target = MRS
                type = MRS_Civil_War
              }
            }
          }
        }
        if = {
          limit = {
            country_exists = MRS
          }
          MRS = {
            declare_war_on = {
              target = NRM
              type = MRS_Civil_War
            }
          }
        }
      }
    }
  }

  # 招募民兵
  MRS_zhao_mu_min_bing = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
        tag = NRM
      }
    }
    fire_only_once = no
    days_remove = 15
    cost = 30
    available = {
      num_divisions < 40
    }
    modifier = {
      weekly_manpower = -1
    }
    remove_effect = {
      ROOT = {
        random_owned_controlled_state = {
          create_unit = {
            division = "name = \"内战民兵\" division_template = \"内战民兵\" start_experience_factor = 0.5"
            owner = ROOT
          }
        }
        random_owned_controlled_state = {
          create_unit = {
            division = "name = \"内战民兵\" division_template = \"内战民兵\" start_experience_factor = 0.5"
            owner = ROOT
          }
        }
      }
    }
  }

  # 购买步兵装备
  MRS_gou_mai_bu_bing_zhuang_bei = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
        tag = NRM
      }
    }
    fire_only_once = no
    days_remove = 15
    cost = 15

    remove_effect = {
      add_equipment_to_stockpile = {
        type = magic_core_a
        amount = 200
        producer = ROOT
      }
    }
  }

  # 对魔女联盟发起宣传攻势
  MRS_dui_MRS_fa_qi_xuan_chuan_gong_shi = {
    allowed = {
      tag = ALI
    }
    visible = {
      OR = {
        has_completed_focus = ALI_fa_dong_xuan_chuan_gong_shi
      }
      MRS = {
        exists = yes
        is_puppet = no
        NOT = {
          has_idea = ali_xuan_chuan_gong_shi_idea
        }
      }
    }
    fire_only_once = yes
    days_remove = 7
    cost = 10

    cancel_trigger = {
      MRS = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    remove_effect = {
      MRS = {
        add_timed_idea = {
          idea = ali_xuan_chuan_gong_shi_idea
          days = 30
        }
      }
    }
  }

  # 对魔法之森联合体发起宣传攻势
  MRS_dui_NRM_fa_qi_xuan_chuan_gong_shi = {
    allowed = {
      tag = ALI
    }
    visible = {
      OR = {
        has_completed_focus = ALI_fa_dong_xuan_chuan_gong_shi
      }
      NRM = {
        exists = yes
        is_puppet = no
        NOT = {
          has_idea = ali_xuan_chuan_gong_shi_idea
        }
      }
    }
    fire_only_once = yes
    days_remove = 7
    cost = 10

    cancel_trigger = {
      NRM = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    remove_effect = {
      NRM = {
        add_timed_idea = {
          idea = ali_xuan_chuan_gong_shi_idea
          days = 30
        }
      }
    }
  }

  # 接触魔女联盟的激进同情者
  MRS_jie_chu_MRS_de_ji_jin_tong_qing_zhe = {
    allowed = {
      tag = NRM
    }
    visible = {
      OR = {
        has_completed_focus = NRM_jie_chu_ji_jin_tong_qing_zhe
      }
      MRS = {
        exists = yes
        is_puppet = no
      }
    }
    fire_only_once = yes
    days_remove = 7
    cost = 10

    cancel_trigger = {
      MRS = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    remove_effect = {
      MRS = {
        random_owned_controlled_state = {
          create_unit = {
            division = "name = \"魔法使激进同情者\" division_template = \"内战民兵\" start_experience_factor = 0.5"
            allow_spawning_on_enemy_provs = yes
            owner = NRM
          }
        }
      }
    }
  }

  # 煽动人偶师协会的人偶
  MRS_shan_dong_ALI_de_ren_ou = {
    allowed = {
      tag = NRM
    }
    visible = {
      OR = {
        has_completed_focus = NRM_jie_chu_ji_jin_tong_qing_zhe
      }
      ALI = {
        exists = yes
        is_puppet = no
      }
    }
    fire_only_once = yes
    days_remove = 7
    cost = 10

    cancel_trigger = {
      ALI = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    remove_effect = {
      ALI = {
        random_owned_controlled_state = {
          create_unit = {
            division = "name = \"人偶反叛部队\" division_template = \"内战民兵\" start_experience_factor = 0.5"
            allow_spawning_on_enemy_provs = yes
            owner = NRM
          }
        }
      }
    }
  }

  # 对魔女联盟发起最终攻势
  MRS_dui_MRS_fa_qi_zui_zhong_gong_shi = {
    allowed = {
      OR = {
        tag = ALI
        tag = NRM
      }
    }
    visible = {
      OR = {
        has_completed_focus = NRM_zhun_bei_zui_hou_yi_ji
        has_completed_focus = ALI_xiang_sheng_li_jin_jun
      }
      MRS = {
        exists = yes
        is_puppet = no
      }
    }
    fire_only_once = yes
    days_remove = 30
    cost = 10

    cancel_trigger = {
      MRS = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    targeted_modifier = {
      tag = MRS
      attack_bonus_against = 0.4
      breakthrough_bonus_against = 0.4
      defense_bonus_against = 0.2
    }

    remove_effect = {
      if = {
        limit = {
          NOT = {
            has_idea = mrs_shi_bai_de_zui_zhong_gong_shi_idea
          }
        }
        add_ideas = mrs_shi_bai_de_zui_zhong_gong_shi_idea
      }
    }
  }

  # 对人偶师协会发起最终攻势
  MRS_dui_ALI_fa_qi_zui_zhong_gong_shi = {
    allowed = {
      OR = {
        tag = MRS
        tag = NRM
      }
    }
    visible = {
      OR = {
        has_completed_focus = NRM_zhun_bei_zui_hou_yi_ji
      }
      ALI = {
        exists = yes
        is_puppet = no
      }
    }
    fire_only_once = yes
    days_remove = 30
    cost = 10

    cancel_trigger = {
      ALI = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    targeted_modifier = {
      tag = ALI
      attack_bonus_against = 0.4
      breakthrough_bonus_against = 0.4
      defense_bonus_against = 0.2
    }

    remove_effect = {
      if = {
        limit = {
          NOT = {
            has_idea = mrs_shi_bai_de_zui_zhong_gong_shi_idea
          }
        }
        add_ideas = mrs_shi_bai_de_zui_zhong_gong_shi_idea
      }
    }
  }

  # 对魔法之森联合体发起最终攻势
  MRS_dui_NRM_fa_qi_zui_zhong_gong_shi = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
      }
    }
    visible = {
      OR = {
        has_completed_focus = ALI_xiang_sheng_li_jin_jun
      }
      NRM = {
        exists = yes
        is_puppet = no
      }
    }
    fire_only_once = yes
    days_remove = 30
    cost = 10

    cancel_trigger = {
      NRM = {
        OR = {
          exists = no
          is_puppet = yes
        } 
      }
    }

    targeted_modifier = {
      tag = NRM
      attack_bonus_against = 0.4
      breakthrough_bonus_against = 0.4
      defense_bonus_against = 0.2
    }

    remove_effect = {
      if = {
        limit = {
          NOT = {
            has_idea = mrs_shi_bai_de_zui_zhong_gong_shi_idea
          }
        }
        add_ideas = mrs_shi_bai_de_zui_zhong_gong_shi_idea
      }
    }
  }
}

MRS_mo_fa_zhi_sen_chong_jian = {
  # 削弱反对派
  MRS_xue_ruo_fan_dui_pai = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
        tag = NRM
      }
    }
    visible = {
      has_idea = mrs_fan_dui_zhe_di_kang_yun_dong_idea
    }
    fire_only_once = yes
    days_remove = 45
    modifier = {
      weekly_manpower = -2
      political_power_cost = 0.7
      stability_factor = -0.1
    }
    cancel_if_not_visible = yes
    remove_effect = {
      swap_ideas = {
        remove_idea = mrs_fan_dui_zhe_di_kang_yun_dong_idea
        add_idea = mrs_bei_xue_ruo_de_fan_dui_zhe_idea
      }
    }
  }
  # 拖延TKM
  MRS_tuo_yan_TKM = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
        tag = NRM
      }
    }
    visible = {
      TKM = {
        has_decision = TKM_Witch_hunt_dec
      }
    }
    cost = 50
    fire_only_once = yes
    complete_effect = {
      TKM = {
        add_days_remove = {
          decision = TKM_Witch_hunt_dec
          days = 90
        }
      }
    }
  }
  # 镇压抵抗
  MRS_zhen_ya_di_kang = {
    state_target = yes
    target_root_trigger = {
        has_global_flag = MRS_civil_war_end_flag
    }
    targets = { 138 552 153 468 174 188 487 354 169 191 206 465 170 172 185 162 154 175 355 182 }
    target_trigger = {
      FROM = {
        NOT = {
          is_core_of = ROOT
        }
      }
    }
    available = {
      FROM = {
        resistance > 30
      }
    }
    on_map_mode = map_only
    icon = GFX_decision_generic_police_action
    cost = 10
    days_remove = 10
    remove_effect = {
        FROM = {
          add_resistance = -20
        }
    }
  } 
  # 增进合作
  MRS_zeng_jin_he_zuo = {
    state_target = yes
    target_root_trigger = {
        has_global_flag = MRS_civil_war_end_flag
    }
    targets = { 138 552 153 468 174 188 487 354 169 191 206 465 170 172 185 162 154 175 355 182 }
    target_trigger = {
      FROM = {
        NOT = {
          is_core_of = ROOT
        }
      }
    }
    available = {
      FROM = {
        compliance < 50
      }
    }
    on_map_mode = map_only
    icon = GFX_decision_hol_exchange_intelligence_data
    cost = 15
    days_remove = 10
    remove_effect = {
        FROM = {
            add_compliance = 30
        }
    }
  } 
  # 恢复秩序
  MRS_hui_fu_zhi_xu = {
    state_target = yes
    target_root_trigger = {
        has_global_flag = MRS_civil_war_end_flag
    }
    targets = { 138 552 153 468 174 188 487 354 169 191 206 465 170 172 185 162 154 175 355 182 }
    target_trigger = {
      FROM = {
        NOT = {
          is_core_of = ROOT
        }
        any_neighbor_state = {
          is_core_of = ROOT
        }
      }
    }
    available = {
      FROM = {
        compliance > 50
      }
    }
    on_map_mode = map_only
    icon = GFX_decision_generic_form_nation
    cost = 5
    days_remove = 30
    remove_effect = {
        FROM = {
            add_core_of = ROOT
        }
    }
  } 

  # 整合魔法之森联合体
  MRS_zheng_he_mo_fa_zhi_sen_lian_he_ti = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
      }
    }
    visible = {
      country_exists = NRM
      NRM = {
        is_puppet_of = ROOT
      }
    }
    available = {
      NRM = {
        all_core_state = {
          is_core_of = ROOT
        }
      }
    }
    cost = 20
    fire_only_once = yes
    days_remove = 15
    remove_effect = {
      custom_effect_tooltip = MRS_integrate_NRM_tt
      hidden_effect = {
        NRM = {
          every_character = {
            set_nationality = ROOT
            clr_character_flag = nationality_NRM
            set_character_flag = nationality_@ROOT
          }
        }
        NRM = {
          low_puppet_level = yes
          low_puppet_level = yes
          low_puppet_level = yes
          low_puppet_level = yes
        }
        promote_character = {
          character = MRS_narumi
          ideology = fandong_type
        }
      }
    }
  }

  # 整合人偶师协会
  MRS_zheng_he_ren_ou_shi_xie_hui = {
    allowed = {
      OR = {
        tag = MRS
        tag = NRM
      }
    }
    visible = {
      country_exists = ALI
      ALI = {
        is_puppet_of = ROOT
      }
    }
    available = {
      ALI = {
        all_core_state = {
          is_core_of = ROOT
        }
      }
    }
    cost = 20
    fire_only_once = yes
    days_remove = 15
    remove_effect = {
      custom_effect_tooltip = MRS_integrate_ALI_tt
      hidden_effect = {
        ALI = {
          every_character = {
            set_nationality = ROOT
            clr_character_flag = nationality_ALI
            set_character_flag = nationality_@ROOT
          }
        }
        ALI = {
          low_puppet_level = yes
          low_puppet_level = yes
          low_puppet_level = yes
          low_puppet_level = yes
        }
        promote_character = {
          character = ALI_ali
          ideology = qinwai_type
        }
        promote_character = {
          character = ALI_ali
          ideology = neworder_type
        }
        promote_character = {
          character = ALI_ali
          ideology = oldorder_type
        }
      }
    }
  }

  # 整合魔女联盟
  MRS_zheng_he_mo_nv_lian_meng = {
    allowed = {
      OR = {
        tag = ALI
        tag = NRM
      }
    }
    visible = {
      country_exists = MRS
      MRS = {
        is_puppet_of = ROOT
      }
    }
    available = {
      MRS = {
        all_core_state = {
          is_core_of = ROOT
        }
      }
    }
    cost = 20
    fire_only_once = yes
    days_remove = 15
    remove_effect = {
      custom_effect_tooltip = MRS_integrate_MRS_tt
      hidden_effect = {
        MRS = {
          every_character = {
            set_nationality = ROOT
            clr_character_flag = nationality_MRS
            set_character_flag = nationality_@ROOT
          }
        }
        MRS = {
          low_puppet_level = yes
          low_puppet_level = yes
          low_puppet_level = yes
          low_puppet_level = yes
        }
        promote_character = {
          character = MRS_narumi
          ideology = fandong_type
        }
        if = {
          limit = {
              tag = NRM
          }
          promote_character = {
            character = ALI_ali
            ideology = qinwai_type
          }
          promote_character = {
            character = ALI_ali
            ideology = neworder_type
          }
          promote_character = {
            character = ALI_ali
            ideology = oldorder_type
          }
        }
        promote_character = {
          character = MRS_mrs
          ideology = guli_type
        }
      }
    }
  }

  # 魔法之森再统一
  MRS_mo_fa_zhi_sen_zai_tong_yi = {
    allowed = {
      OR = {
        tag = MRS
        tag = ALI
        tag = NRM
      }
    }
    available = {
      custom_trigger_tooltip = {
        tooltip = MRS_reunite_MRS_trigger_tt
      }
      hidden_trigger = {
        468 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        174 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        188 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        487 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        172 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        138 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        552 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        153 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        465 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        170 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        185 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        162 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        154 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        354 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        169 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        191 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        175 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        355 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        182 = {
          is_core_of = ROOT
          is_owned_by = ROOT
        }
        206 = {
          OR = {
            AND = {
              is_core_of = ROOT
              is_owned_by = ROOT
            }
            NOT = {
              is_owned_by = MRS
              is_owned_by = ALI
              is_owned_by = NRM
            }
          }
        }
      }
    }
    cost = 20
    fire_only_once = yes
    complete_effect = {
      add_political_power = 300
      add_ideas = mrs_zai_tong_yi_de_mo_fa_zhi_sen_idea
      add_ideas = Country
      if = {
        limit = {
          has_idea = mrs_fan_dui_zhe_di_kang_yun_dong_idea
        }
        remove_ideas = mrs_fan_dui_zhe_di_kang_yun_dong_idea
      }
      if = {
        limit = {
          has_idea = mrs_bei_xue_ruo_de_fan_dui_zhe_idea
        }
        remove_ideas = mrs_bei_xue_ruo_de_fan_dui_zhe_idea
      }
      if = {
        limit = {
          has_idea = mrs_bushimofashi
        }
        remove_ideas = mrs_bushimofashi
      }
      hidden_effect = {
        if = {
          limit = {
            NOT = {
              tag = MRS
            }
          }
          MRS = {
            every_core_state = {
              remove_core_of = MRS
            }
          }
        }
        if = {
          limit = {
            NOT = {
              tag = ALI
            }
          }
          ALI = {
            every_core_state = {
              remove_core_of = ALI
            }
          }
        }
        if = {
          limit = {
            NOT = {
              tag = NRM
            }
          }
          NRM = {
            every_core_state = {
              remove_core_of = NRM
            }
          }
        }
      }
    }
  }
}

# 战争计划决议组
MRS_fang_yu_zhan_zheng_ji_hua = {
  # 战争计划
  MRS_dui_FROM_de_zhan_zheng_ji_hua = {
    target_root_trigger = {
        has_completed_focus = MRS_zhan_zheng_ji_hua_bu
    }
    target_array = global.countries
    targets_dynamic = yes
    target_trigger = {
      FROM = {
        has_offensive_war_with = MRS
        NOT = {
          has_country_flag = MRS_has_completed_war_plan
        }
      }
    }
    cost = 30
    days_remove = 90

    targeted_modifier = {
      tag = FROM
      attack_bonus_against_cores = 0.2
      defense_bonus_against = 0.2
    }

    remove_effect = {
      FROM = {
        set_country_flag = MRS_has_completed_war_plan
      }
    }
  }
}

MRS_Offer_peace_MSD = {
  MRS_Offer_peace_MSD_dec = {
    allowed = {
      original_tag = MRS
    }
    visible = {    
      has_war_with = MSD   
      MSD = { has_idea = MRS_Kemonomichi_war_idea_1 }
      MRS = { has_idea = MRS_Kemonomichi_war_idea_2 }
    }
    target_trigger = {
      FROM = {
        original_tag = MSD
      }
    }
    targets = { MSD }

    available = {
      original_tag = MRS
      has_war_with = MSD
      MSD = { has_idea = MRS_Kemonomichi_war_idea_1 }
      MRS = { has_idea = MRS_Kemonomichi_war_idea_2 }

      or = {
        MRS = { surrender_progress > 0.4 }
        and = {
          or = {
            and = {
              date > 147.3.1
              MSD = {
                not = { has_war_with = HKR}
              }
            }
            and = {
              date > 147.7.1
              MSD = {
                has_war_with = HKR
              }
            }
          }	
          or = {
            182 = {
              is_controlled_by = MSD
            }
            206 = {
              is_controlled_by = MSD
            }
            355 = {
              is_controlled_by = MSD
            }
            175 = {
              is_controlled_by = MSD
            }
          }
        }
        and = {
          182 = {
            is_controlled_by = MSD
          }
          206 = {
            is_controlled_by = MSD
          }
          355 = {
            is_controlled_by = MSD
          }
          175 = {
            is_controlled_by = MSD
          }
        }
      }


    }

    cost = 0

    fire_only_once = yes

    ai_will_do = {
      factor = 1000
    }
  
    complete_effect = {

      if = {
        limit = {
          NOT = { date > 147.3.1 }
        }
        add_political_power = -100
        add_stability = -0.05
      }

      army_experience = 20
      MSD = {
        set_state_owner = 206
      }
      white_peace = MSD

      hidden_effect = {
        country_event = { id = mrs.46 days = 60 random = 600}
      }

    }
  }

  MRS_Declare_our_victory_dec = {
    allowed = {
      original_tag = MRS
    }
    visible = {       
      has_war_with = MSD
      MSD = { has_idea = MRS_Kemonomichi_war_idea_1 }
      MRS = { has_idea = MRS_Kemonomichi_war_idea_2 }
    }

    target_trigger = {
      FROM = {
        original_tag = MSD
      }
    }
    targets = { MSD }

    available = {
      original_tag = MRS
      has_war_with = MSD
      MSD = { has_idea = MRS_Kemonomichi_war_idea_1 }
      MRS = { has_idea = MRS_Kemonomichi_war_idea_2 }

      or = {
        and = {
          date > 147.6.1
          MSD = {
            not = { has_war_with = HKR}
          }
        }
        and = {
          date > 147.10.1
          MSD = {
            has_war_with = HKR
          }
        }
      }

      and = {
        182 = {
          NOT = { is_controlled_by = MSD }
        }
        206 = {
          NOT = { is_controlled_by = MSD }
        }
        355 = {
          NOT = { is_controlled_by = MSD }
        }
        175 = {
          NOT = { is_controlled_by = MSD }
        }
      }

    }

    cost = 0

    fire_only_once = yes

    ai_will_do = {
      factor = 1000
    }
  
    complete_effect = {

      add_political_power = 100
      add_war_support = 0.10

      army_experience = 10
      white_peace = MSD

      MSD = {
        add_political_power = -200
        add_stability = -0.20
        add_war_support = -0.30
      }

      hidden_effect = {
        country_event = { id = mrs.46 days = 60 random = 600}
        set_country_flag = MRS_victory_in_MSD_war_flag
      }

    }
  }

}